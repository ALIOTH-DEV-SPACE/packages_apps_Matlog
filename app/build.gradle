apply plugin: 'com.android.application'
android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.pluscubed.logcat"
        minSdkVersion 14
        targetSdkVersion 22
        versionName '1.0.0-beta'
        versionCode 1

        testApplicationId "com.pluscubed.logcat.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"

        manifestPlaceholders = [appName: "@string/app_name"]
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_MATLOG
                keyPassword RELEASE_KEY_PASSWORD_MATLOG
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            manifestPlaceholders = [appName: "MatLog DEBUG"]
            ext.enableCrashlytics = false
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.+'
    compile 'com.android.support:support-annotations:22.+'
    compile 'com.android.support:design:22.+'
    compile 'com.afollestad:material-dialogs:0.+'
}